buildscript {
	repositories {
		jcenter()
		maven {
			name "forge"
			url "http://files.minecraftforge.net/maven/"
		}
		maven {
			name = "jitpack"
			url = "https://jitpack.io"
		}
	}
	dependencies {
		classpath 'com.github.CDAGaming:ForgeGradle:1c670759c5'
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.10"
		classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
	}
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'forge'
apply plugin: 'kotlin'

file "gradle.properties" withReader {
	def prop = new Properties()
	prop.load(it)
	ext.config = new ConfigSlurper().parse prop
}

version = config.mod_version

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

minecraft {
	version = config.mc_version + "-" + config.forge_version + "-" + config.mc_version
	runDir = "run"

	replaceIn "Forgelin.kt"
	replace "@VERSION@", config.mod_version
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from (sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": project.version, "mcversion": config.mc_version
	}
	from (sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}

repositories {
	jcenter()
}

dependencies {
	compile group: "org.jetbrains.kotlin", name: "kotlin-stdlib", version: config.kotlin_version
	compile group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk7", version: config.kotlin_version
	compile group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk8", version: config.kotlin_version
	compile group: "org.jetbrains.kotlin", name: "kotlin-reflect", version: config.kotlin_version
	compile group: "org.jetbrains", name: "annotations", version: config.annotations_version
	compile group: "org.jetbrains.kotlinx", name: "kotlinx-coroutines-core", version: config.coroutines_version
	compile group: "org.jetbrains.kotlinx", name: "kotlinx-coroutines-jdk8", version: config.coroutines_version
}

jar {
	manifest {
		attributes "FMLCorePlugin": "net.shadowfacts.forgelin.preloader.ForgelinPlugin",
					"FMLCorePluginContainsFMLMod": "true"
	}
}

shadowJar {
	classifier = ""
	dependencies {
		exclude 'net\\minecraftforge\\**'
		exclude 'GradleStart*'
		include(dependency("org.jetbrains.kotlin:kotlin-stdlib:${config.kotlin_version}"))
		include(dependency("org.jetbrains.kotlin:kotlin-stdlib-jdk7:${config.kotlin_version}"))
		include(dependency("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${config.kotlin_version}"))
		include(dependency("org.jetbrains.kotlin:kotlin-reflect:${config.kotlin_version}"))
		include(dependency("org.jetbrains:annotations:${config.annotations_version}"))
		include(dependency("org.jetbrains.kotlinx:kotlinx-coroutines-core:${config.coroutines_version}"))
		include(dependency("org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:${config.coroutines_version}"))
	}
	manifest {
		attributes "FMLCorePlugin": "net.shadowfacts.forgelin.preloader.ForgelinPlugin",
					"FMLCorePluginContainsFMLMod": "true"
	}
}

tasks.build.dependsOn shadowJar

tasks.withType(JavaCompile) {
    options.compilerArgs.addAll(['-Xlint:unchecked', '-Xlint:deprecation'])
}

artifacts {
	archives shadowJar
}

reobf {
	shadowJar {
	}
}

task reobfShadowJar(dependsOn: 'reobf'){
}

tasks.reobfShadowJar.mustRunAfter shadowJar
tasks.build.dependsOn reobfShadowJar